<?xml version="1.0" encoding="utf-8"?>
<mx:ViewStack xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" xmlns:mapping="mapping.*" 
	creationComplete="getSemregService.send();" label="{filename}">
	<mx:Script>
	<![CDATA[
		import mx.events.CloseEvent;
		import mx.events.CollectionEvent;
		import mx.rpc.events.ResultEvent;
		import dart.mapping.TableThumbEvent;
		import dart.mapping.JoinEvent;
		import mx.controls.Alert;
		//import flash.net.URLVariables;

        [Bindable]
		private var tableFeed:XML;
			
		private var _isChange:Boolean = false;
		public function set isChange(c:Boolean):void {
			_isChange = c;
			this.label = filename + (_isChange?'*':'');
			tableData.source = tableFeed.children().(localName()=='table'||localName()=='view');
		}
		public function get isChange():Boolean {return _isChange;}
		
		[Bindable]
		public var filename:String = "tcmct.semreg";
		public static var prefix:String = "http://ccnt.cn/tcm";
		private function tableServiceResultHandler(event:ResultEvent):void {
            tableFeed = event.result as XML;
            tables.addEventListener(TableThumbEvent.ADD, tableThumbEventHandler);
            tables.addEventListener(TableThumbEvent.BROWSE, tableThumbEventHandler);
            tables.addEventListener(TableThumbEvent.DELETE, tableThumbEventHandler);
            detail.addEventListener(TableThumbEvent.UPDATE, tableThumbEventHandler);
            detail.addEventListener(JoinEvent.ADD, joinEventHandler);
        	detail.addEventListener(JoinEvent.EDIT, joinEventHandler);
        }
        private function tableThumbEventHandler(event:TableThumbEvent):void {
        	var type:String = event.table.localName();
        	if (event.type == TableThumbEvent.ADD) {
        		var newTable:XML;
        		if(type == 'table')
        			newTable = XML("<table><uri>{"+prefix+"}"+event.table.@localName+"</uri><ontologies/></table>");
        		else if(type == 'view')
        			newTable = XML("<view><uri>{"+prefix+"}."+"</uri><ontologies/><tables/></view>");
        		tableFeed.appendChild(newTable);
        		isChange = true;
        		if(type == 'view'){
        			detail.table = newTable;
        			selectedChild = detail;
        			detail.viewEditor.selectedIndex = 1;
                	this.parentDocument.selectAccordion(0);        			
        		}        			
        	} else if (event.type == TableThumbEvent.BROWSE) {
                detail.table = event.table;
                selectedChild = detail;
                detail.viewEditor.selectedIndex = 0;
                this.parentDocument.selectAccordion(1);
            } else if (event.type == TableThumbEvent.DELETE) {
                detail.table = event.table;
                var str:String = detail.table.uri;
	    		str = str.substring(str.lastIndexOf("}")+1);
	    		if(type == 'table')
                	Alert.show("确定将表\""+str+"\"从注册文件中删除？","删除注册表", 3, this, deleteTable);
                else if(type == 'view')
                	Alert.show("确定将视图\""+str+"\"从注册文件中删除？","删除注册视图", 3, this, deleteTable);
            } else if (event.type == TableThumbEvent.UPDATE) {
            	//Alert.show(event.table.toString(),"update in Semreg file");//show
                for(var i:Number=0; i < tableData.length; i++) {
                    if(tableData[i].uri == event.table.uri) {
                        tableData[i] = event.table;
        				isChange = true;
                    }
                }
            } else {
                dispatchEvent(event);
            }
        }
        private function deleteTable(event:CloseEvent):void {
        	if (event.detail == Alert.YES){
	    		var children:XMLList = tableFeed.child(detail.table.localName());
                for(var i:Number=0; i < children.length(); i++) {
                    if(children[i].uri == detail.table.uri) {
                        delete children[i];
        				isChange = true;
                    }
                }
             }
        }
        
        private function joinEventHandler(event:JoinEvent):void {            
            //Alert.show(event.join,"action:"+event.type);//
            var children:XMLList = tableFeed.join;
            //Alert.show(joinData.toString(),"length:"+children.length());//
            for(var i:Number=0; i < children.length(); i++) {
            	if(children[i].@id == event.join.@id){
            		children[i] = event.join;
            		break;
            	}
            }
            if(children.length() == 0 || i == children.length())
            	tableFeed.appendChild(event.join);
            isChange = true;
        }
        
        public function returnTileView():void {
        	if(this.selectedIndex == 2)//xml
        		tableFeed = new XML(content.text);
        	selectedChild = tables;
        	this.parentDocument.selectAccordion(0);
        }
        public function returnXmlView():void {
        	selectedChild = xml;
        	content.text = tableFeed.toString();
        	///this.parentDocument.selectAccordion(2);
        }
        /********************save*************************/
        public function save():void{
        	semregRO.save(filename,tableFeed.toString());
        }
        /******************handler************************/
        private function semregROServiceResultHandler(event:ResultEvent):void {
        	isChange = false;
        }
	]]>
	</mx:Script>
	<mapping:TableTiles id="tables"/>
	<mapping:TableEditor id="detail"/>
	<mx:Canvas width="100%" height="100%" id="xml">
		<mx:TextArea id="content" width="100%" height="100%" text="{tableFeed.toString()}" change="{isChange=true}"/>
	</mx:Canvas>
	
	<mx:Sort id="uriSort">
		<mx:fields>
			<mx:SortField name="uri" caseInsensitive="true" />
		</mx:fields>
	</mx:Sort>
	<mx:XMLListCollection id="tableData" sort="{uriSort}" source="{tableFeed.children().(localName()=='table'||localName()=='view')}"/>
	<mx:XMLListCollection id="joinData" source="{tableFeed.join}"/>
	<mx:HTTPService id="getSemregService" url="./getSemreg?filename={filename}" useProxy="false" resultFormat="e4x" result="tableServiceResultHandler(event)" />
	<mx:RemoteObject id="semregRO" destination="semregController" showBusyCursor="true" result="semregROServiceResultHandler(event)"/>
</mx:ViewStack>
